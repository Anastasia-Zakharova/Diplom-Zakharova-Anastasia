#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
//ВКМ_Захарова: Получить структуру (УправлениеПечатьюПереопределяемый.ПриПолученииПодписейИПечатей)

Функция ПолучитьПодписиИПечати(Организация) Экспорт 
//ВКМ_Захарова: Получить структуру (УправлениеПечатьюПереопределяемый.ПриПолученииПодписейИПечатей)
	
	НаборКартинок = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.ВКМ_ФайлКартинкиПечать КАК ПечатьОрганизации,
		|	Организации.ВКМ_ФайлКартинкиПодпись КАК ПодписьРуководителя
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		КартинкаПечать = КартинкаИзФайла(Выборка.ПечатьОрганизации);
		НаборКартинок.Вставить("ПечатьОрганизации", КартинкаПечать);
		
		КартинкаПодпись = КартинкаИзФайла(Выборка.ПодписьРуководителя);
		НаборКартинок.Вставить("ПодписьРуководителя", КартинкаПодпись);
		
	Иначе
		
		НаборКартинок.Вставить("ПечатьОрганизации", Новый Картинка);
		НаборКартинок.Вставить("ПодписьРуководителя", Новый Картинка);
		
	КонецЕсли;
	
	Возврат НаборКартинок;
	
//ВКМ_Захарова
КонецФункции

//ВКМ_Захарова
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//ВКМ_Захарова: Получить картинку подписи и печати

Функция КартинкаИзФайла(Файл)
//ВКМ_Захарова: Получить картинку подписи и печати
	
	ДвоичныеДанные = Неопределено;
	
	Если ЗначениеЗаполнено(Файл) Тогда
		ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(Файл, Ложь);
	КонецЕсли;
	
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат Новый Картинка;
	КонецЕсли;
	
	Возврат Новый Картинка(ДвоичныеДанные, Истина);
	
//ВКМ_Захарова
КонецФункции

//ВКМ_Захарова
#КонецОбласти

#КонецЕсли


