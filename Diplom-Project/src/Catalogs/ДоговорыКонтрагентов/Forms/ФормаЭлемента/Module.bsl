
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
//ВКМ_Захарова: Доработка формы
	
	УстановитьДействиеПриИзмененииВидаДоговора();
	
	ДоработатьФормуОтносительноВидаДоговора();
	
//ВКМ_Захарова
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ВКМ_ВидДоговораПриИзменении(Элемент)
//ВКМ_Захарова: Доработка формы
	
	ДоработатьФормуОтносительноВидаДоговора();
	
//ВКМ_Захарова
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВКМ_ПериодДействияДоговораПриИзменении(Элемент)
//ВКМ_Захарова: Установить дату начала и окончания действия договора
	
	Объект.ВКМ_ДатаНачалаДействияДоговора= НачалоДня(ЭтотОбъект.ВКМ_ПериодДействияДоговора.ДатаНачала);
	Объект.ВКМ_ДатаОкончанияДействияДоговора = КонецДня(ЭтотОбъект.ВКМ_ПериодДействияДоговора.ДатаОкончания);
	
//ВКМ_Захарова
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьДействиеПриИзмененииВидаДоговора()
//ВКМ_Захарова: Установить действие
	
	Элементы.ВидДоговора.УстановитьДействие("ПриИзменении","Подключаемый_ВКМ_ВидДоговораПриИзменении");
	
//ВКМ_Захарова
КонецПроцедуры

&НаСервере
Процедура ДоработатьФормуОтносительноВидаДоговора()
//ВКМ_Захарова: Доработка формы в зависимости от выбранного вида договора
	
	ЭтоАбонентскоеОбслуживание = (Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	
	Если ЭтоАбонентскоеОбслуживание Тогда
		ВКМ_ДоработкаФормыДоговоры.ДоработатьФормуСправочникаДоговорыКонтрагентов(ЭтотОбъект);
		УстановитьПериодДействияДоговора();
	Иначе
		Объект.ВКМ_СуммаАбонентскойПлаты = 0;
		Объект.ВКМ_СтоимостьЧасаРаботы = 0;
		Объект.ВКМ_ДатаНачалаДействияДоговора = Дата('00010101');
		Объект.ВКМ_ДатаОкончанияДействияДоговора = Дата('00010101');
	КонецЕсли;
	
	ИмяГруппы = "ГруппаАбоненскоеОбслуживание";
	ГруппаФормы = Элементы.Найти(ИмяГруппы);
	Если ГруппаФормы <> Неопределено Тогда
		Элементы.ГруппаАбоненскоеОбслуживание.Видимость = ЭтоАбонентскоеОбслуживание;
	КонецЕсли;
	
//ВКМ_Захарова
КонецПроцедуры

&НаСервере
Процедура УстановитьПериодДействияДоговора()
//ВКМ_Захарова: Установить период действия договора
	
	ЭтотОбъект.ВКМ_ПериодДействияДоговора.ДатаНачала = Объект.ВКМ_ДатаНачалаДействияДоговора;
	ЭтотОбъект.ВКМ_ПериодДействияДоговора.ДатаОкончания = Объект.ВКМ_ДатаОкончанияДействияДоговора;
	
//ВКМ_Захарова
КонецПроцедуры

#КонецОбласти