
#Область ПрограммныйИнтерфейс

// Заполнить список реализаций по договорам.
// 
// Параметры:
//  ВыбранныйПериод - Дата - Выбранный период
// 
// Возвращаемое значение:
//  ТаблицаЗначений -  Заполнить список реализаций по договорам:
// * СписокДоговоров - СправочникСсылка.ДоговорыКонтрагентов
// * СписокРеализаций - ДокументСсылка.РеализацияТоваровУслуг 
Функция ЗаполнитьСписокРеализацийПоДоговорам(ВыбранныйПериод) Экспорт
	
	ТаблицаДокументы = Новый ТаблицаЗначений;
	ТаблицаДокументы.Колонки.Добавить("СписокДоговоров");
	ТаблицаДокументы.Колонки.Добавить("СписокРеализаций");
	
	РезультатЗапроса = ПроверитьДанныеАктовРеализаций(ВыбранныйПериод);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТаблицаДокументы;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		НовыйДокумент.Дата = ВыбранныйПериод;
		НовыйДокумент.Организация = Выборка.Организация;
		НовыйДокумент.Контрагент = Выборка.Контрагент;
		НовыйДокумент.Договор = Выборка.Договор;
		НовыйДокумент.Ответственный = ПараметрыСеанса.ТекущийПользователь;
		
		НовыйДокумент.ВыполнитьАвтозаполнение();
		
		Если ЗначениеЗаполнено(НовыйДокумент.Услуги) Тогда
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			НовыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		КонецЕсли;
		
		НоваяСтрокаТаблицы = ТаблицаДокументы.Добавить();
		НоваяСтрокаТаблицы.СписокДоговоров = Выборка.Договор;
		НоваяСтрокаТаблицы.СписокРеализаций = НовыйДокумент.Ссылка;
		
	КонецЦикла;
	
	Возврат ТаблицаДокументы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроверитьДанныеАктовРеализаций(ВыбранныйПериод)
	
	//Замечание №5. Отбор по датам
	НачалоВыбранногоПериода = НачалоМесяца(ВыбранныйПериод);
	КонецВыбранногоПериода = КонецМесяца(ВыбранныйПериод);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ ВТ_Договоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора >= &КонецВыбранногоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК СсылкаРеализация
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	НЕ РеализацияТоваровУслуг.ПометкаУдаления
	|	И РеализацияТоваровУслуг.Проведен
	|	И РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоВыбранногоПериода И &КонецВыбранногоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Реализации.СсылкаРеализация КАК СсылкаРеализация,
	|	ВТ_Договоры.Договор.Организация КАК Организация,
	|	ВТ_Договоры.Договор.Владелец КАК Контрагент,
	|	ВТ_Договоры.Договор КАК Договор
	|ИЗ
	|	ВТ_Договоры КАК ВТ_Договоры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|		ПО ВТ_Договоры.Договор = ВТ_Реализации.СсылкаРеализация.Договор
	|ГДЕ
	|	ВТ_Реализации.СсылкаРеализация ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("НачалоВыбранногоПериода", НачалоВыбранногоПериода);
	Запрос.УстановитьПараметр("КонецВыбранногоПериода", КонецВыбранногоПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

#КонецОбласти
