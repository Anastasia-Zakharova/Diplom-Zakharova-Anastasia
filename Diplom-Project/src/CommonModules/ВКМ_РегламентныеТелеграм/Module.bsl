
#Область ПрограммныйИнтерфейс

// Сообщения телеграм бот.
// 
// Возвращаемое значение:
//  Неопределено, Произвольный -  Сообщения телеграм бот
Функция СообщенияТелеграмБот() Экспорт
	
	ОтправитьСообщенияТелеграм();
	
	//Константа ВКМ_УникальныйИдентификаторОбновления - это update_id (позволяет игнорировать повторные обновления)
	ИсходныйИдентификаторОбновления = ВКМ_СлужебныеТелеграм.ПолучитьУникальныйИдентификаторОбновления();
	
	ИдентификаторОбновления_offset = ИсходныйИдентификаторОбновления + 1;
	
	ОтветБота = ВКМ_МетодыТелеграм.ПолучитьОбновления_getUpdates(ИдентификаторОбновления_offset);
	
	Если ОтветБота = Неопределено Или ОтветБота["result"] = Неопределено Тогда
		Возврат ОтветБота;
	КонецЕсли;
	
	Для Каждого НовоеСообщение Из ОтветБота["result"] Цикл
		
		Если Не НовоеСообщение.Получить("message") = Неопределено Тогда
			ДанныеСообщения = НовоеСообщение["message"];
			ОбработатьСообщениеТелеграм(ДанныеСообщения);
		КонецЕсли;
		
		Константы.ВКМ_УникальныйИдентификаторОбновления.Установить(НовоеСообщение["update_id"]);
		
	КонецЦикла;
	
	Возврат ОтветБота;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОтправитьОбработатьСообщениеТелеграм

Процедура ОтправитьСообщенияТелеграм()
	
	ИдентификаторЧатаГруппы_chat_id = ВКМ_СлужебныеТелеграм.ПолучитьИдентификаторЧатаГруппыТелеграм();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВКМ_УведомленияТелеграмБоту.Ссылка";
	
	РезультатЗапросаУведомления = Запрос.Выполнить();
	
	Если Не РезультатЗапросаУведомления.Пустой() Тогда
		
		Выборка = РезультатЗапросаУведомления.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщения_text = Выборка.ТекстСообщения;
			ЯзыкРазметкиТекста_parse_mode = "HTML";
			Результат = ВКМ_МетодыТелеграм.ОтправитьСообщение_sendMessage(ИдентификаторЧатаГруппы_chat_id,
			ТекстСообщения_text, ЯзыкРазметкиТекста_parse_mode);
			
			Если Результат["ok"] Тогда
				УведомлениеТелеграмОбъект = Выборка.Ссылка.ПолучитьОбъект();
				УведомлениеТелеграмОбъект.Удалить();
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьСообщениеТелеграм(ДанныеСообщения)
	
	Если ЗначениеЗаполнено(ДанныеСообщения["from"]) Тогда
		ДанныеОтправителя = ДанныеСообщения["from"];
		ЗафиксироватьОтправителя(ДанныеОтправителя);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеСообщения["text"]) Тогда
		
		ДанныеЧата = ДанныеСообщения["chat"];
		ИдентификаторЧата = ДанныеЧата["id"];
		ИдентификаторОтветаНаСообщение = ДанныеСообщения["message_id"]; //Ответ на исходное сообщение отправки
		
		ТекстСообщенияОтправителя = ДанныеСообщения["text"];
		
		Если СтрНачинаетсяС(ТекстСообщенияОтправителя, "/start") Тогда
			ОтправитьСообщениеПриветствия(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "/foto") Тогда
			ОтправитьФотоИнструкции(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "/audio") Тогда
			ОтправитьАудиоИнструкцию(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "/info") Тогда
			ОтправитьДополнительнуюКлавиатуру(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "Контакты") Тогда
			ОтправитьКонтактыКомпании(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "Адрес") Тогда
			ОтправитьМестоположениеКомпании(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "Реквизиты") Тогда
			ОтправитьРеквизитыКомпании(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		ИначеЕсли СтрНачинаетсяС(ТекстСообщенияОтправителя, "Образец") Тогда
			ОтправитьОбразецЛистаУчета(ИдентификаторЧата, ИдентификаторОтветаНаСообщение);
		Иначе
			ОтправитьСообщениеОбщегоОтвета(ИдентификаторЧата, ИдентификаторОтветаНаСообщение, ТекстСообщенияОтправителя);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗафиксироватьОтправителя

Процедура ЗафиксироватьОтправителя(ДанныеОтправителя)
	
	ИдентификаторОтправителя = ВКМ_СлужебныеТелеграм.ФорматироватьЧислоВСтроку(ДанныеОтправителя["id"]);
	
	УчетнаяЗапись = Справочники.ВКМ_УчетныеЗаписиТелеграм.НайтиПоКоду(ИдентификаторОтправителя);
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиДанныхОтправителя = Новый Массив;
	
	Если ЗначениеЗаполнено(ДанныеОтправителя["first_name"]) Тогда
		ИмяПользователяВЧате = ДанныеОтправителя["first_name"];
		ЧастиДанныхОтправителя.Добавить(ИмяПользователяВЧате);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеОтправителя["last_name"]) Тогда
		ФамилияПользователяВЧате = ДанныеОтправителя["last_name"];
		ЧастиДанныхОтправителя.Добавить(ФамилияПользователяВЧате);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеОтправителя["username"]) Тогда
		ИмяПользователяТелегам = СтрШаблон("@%1", ДанныеОтправителя["username"]);
	КонецЕсли;
	
	ДанныеОтправителя = СтрСоединить(ЧастиДанныхОтправителя, " ");
	
	СправочникОбъект = Справочники.ВКМ_УчетныеЗаписиТелеграм.СоздатьЭлемент();
	СправочникОбъект.Код = ИдентификаторОтправителя;
	СправочникОбъект.Наименование = ДанныеОтправителя;
	СправочникОбъект.ИмяПользователяТелеграм = ИмяПользователяТелегам;
	СправочникОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СообщениеПриветствия

Процедура ОтправитьСообщениеПриветствия(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ТекстПриветствие = "<b>%F0%9F%8E%89 Я чат бот! Приветствую вас! Выберите нужную команду!</b>";
	ТекстФотоИструкция = "<i>%F0%9F%93%8C Фото инструкция /foto_instruction</i>";
	ТекстГолосИструкция = "<i>%F0%9F%93%A2 Аудио инструкция /audio_instruction</i>";
	ТекстДопКлавиатура = "<i>%F0%9F%93%9F Информационная клавиатура /info_keyboard</i>";
	
	ТекстСообщения_text = СтрШаблон("%1%2%3%4%5%6%7",ТекстПриветствие, Символы.ПС, ТекстФотоИструкция,
	Символы.ПС, ТекстГолосИструкция, Символы.ПС, ТекстДопКлавиатура);
	
	ЯзыкРазметкиТекста_parse_mode = "HTML";
	ПредварительныйПросмотрСсылок_disable_web_page_preview = Ложь;
	УведомленияБезЗвука_disable_notification = Ложь;
	
	ВКМ_МетодыТелеграм.ОтправитьСообщение_sendMessage(ИдентификаторЧата_chat_id, ТекстСообщения_text,
		ЯзыкРазметкиТекста_parse_mode, ПредварительныйПросмотрСсылок_disable_web_page_preview,
		УведомленияБезЗвука_disable_notification, ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область ФотоИнструкция

Процедура ОтправитьФотоИнструкции(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ИмяФайла = СтрШаблон("%1telegram.jpg", КаталогВременныхФайлов());
	БиблиотекаКартинок.ВКМ_ФотоИнструкцияТелеграм.Записать(ИмяФайла);
	
	ФотоДляОтправки_photo = Неопределено;
	ПодписьКФотографии_caption = "%F0%9F%93%8C Фото инструкция";
	УведомленияБезЗвука_disable_notification = Ложь;
	
	ВКМ_МетодыТелеграм.ОтправитьФото_sendPhoto(ИмяФайла, ИдентификаторЧата_chat_id, ФотоДляОтправки_photo,
		ПодписьКФотографии_caption, УведомленияБезЗвука_disable_notification,
		ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область АудиоИнструкция

Процедура ОтправитьАудиоИнструкцию(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ИмяФайла = СтрШаблон("%1audio.mp3", КаталогВременныхФайлов());
	МакетАудиоИнструкция = ПолучитьОбщийМакет("ВКМ_АудиоИнструкцияТелеграм");
	МакетАудиоИнструкция.Записать(ИмяФайла);
	
	АудиоДляОтправки_audio = Неопределено;
	ПодписьАудио_caption = "%F0%9F%93%A2 Аудио инструкция";
	ПродолжительностьАудио_duration = Неопределено;
	Исполнитель_performer = "1C_Bot";
	НазваниеТрека_title = "Аудио инструкция";
	
	ВКМ_МетодыТелеграм.ОтправитьАудио_sendAudio(ИмяФайла, ИдентификаторЧата_chat_id, АудиоДляОтправки_audio,
		ПодписьАудио_caption, ПродолжительностьАудио_duration, Исполнитель_performer,
		НазваниеТрека_title,, ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область ДополнительнаяКлавиатура

Процедура ОтправитьДополнительнуюКлавиатуру(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ТекстСообщения_text = "%F0%9F%93%9F Клавиатура отправлена";
	ДополнительнаяКлавиатура_reply_markup = СформироватьКлавиатуру();
	
	ВКМ_МетодыТелеграм.ОтправитьСообщение_sendMessage(ИдентификаторЧата_chat_id, ТекстСообщения_text,,,,
		ИдентификаторОтветаНаСообщение_reply_to_message_id,ДополнительнаяКлавиатура_reply_markup);
	
КонецПроцедуры

Функция СформироватьКлавиатуру()
	
	МассивРядовКлавиатуры_keyboard = Новый Массив;
	МассивКнопок = Новый Массив;
	
	ДобавитьКнопкуГоризонтально(МассивКнопок, "Контакты компании %F0%9F%93%B2");
	ДобавитьКнопкуГоризонтально(МассивКнопок, "Адрес компании %F0%9F%8C%8D");
	ДобавитьКнопкуГоризонтально(МассивКнопок, "Реквизиты компании %F0%9F%93%87");
	
	МассивРядовКлавиатуры_keyboard.Добавить(МассивКнопок);
	
	МассивРядовКлавиатуры_keyboard.Добавить(ДобавитьКнопкуВертикально("Образец листа учёта рабочего времени %F0%9F%93%8B"));
	
	ОптимизацияРазмераКлавиатуры_resize_keyboard = Истина;
	СкрытиеКлавиатурыПослеИспользования_one_time_keyboard = Истина;
	ПоказКлавиатурыОпределенномуПользователю_selective = Истина;
	
	ДополнительнаяКлавиатура = Клавиатура_ReplyKeyboardMarkup(МассивРядовКлавиатуры_keyboard,
		ОптимизацияРазмераКлавиатуры_resize_keyboard, СкрытиеКлавиатурыПослеИспользования_one_time_keyboard,
		ПоказКлавиатурыОпределенномуПользователю_selective);
	
	Возврат ДополнительнаяКлавиатура;
	
КонецФункции

Процедура ДобавитьКнопкуГоризонтально(МассивКнопок, ТекстКнопки)
	
	ДанныеКнопки = Новый Структура;
	ДанныеКнопки.Вставить("text", ТекстКнопки);
	МассивКнопок.Добавить(ДанныеКнопки);
	
КонецПроцедуры

Функция ДобавитьКнопкуВертикально(ТекстКнопки)
	
	МассивКнопок = Новый Массив;
	
	ДанныеКнопки = Новый Структура;
	ДанныеКнопки.Вставить("text", ТекстКнопки);
	МассивКнопок.Добавить(ДанныеКнопки);
	
	Возврат МассивКнопок;
	
КонецФункции

Функция Клавиатура_ReplyKeyboardMarkup(Keyboard, Resize_keyboard = Неопределено, One_time_keyboard = Неопределено, Selective = Неопределено)
	
	ДанныеКлавиатуры = Новый Структура;
	
	ДанныеКлавиатуры.Вставить("keyboard", Keyboard);
	
	Если ЗначениеЗаполнено(Resize_keyboard) Тогда
		ДанныеКлавиатуры.Вставить("resize_keyboard", Resize_keyboard);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(One_time_keyboard) Тогда
		ДанныеКлавиатуры.Вставить("one_time_keyboard", One_time_keyboard);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Selective) Тогда
		ДанныеКлавиатуры.Вставить("selective", Selective);
	КонецЕсли;
	
	Возврат ДанныеКлавиатуры;
	
КонецФункции

#КонецОбласти

#Область КонтактыКомпании

Процедура ОтправитьКонтактыКомпании(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	КонтактыКомпании_phone_number = ВКМ_СлужебныеТелеграм.ПолучитьКонтактыКомпанииТелеграм();
	НазваниеКомпании_first_name = ВКМ_СлужебныеТелеграм.ПолучитьНазваниеКомпанииТелеграм();
	
	ВКМ_МетодыТелеграм.ОтправитьКонтакт_sendContact(ИдентификаторЧата_chat_id, КонтактыКомпании_phone_number,
		НазваниеКомпании_first_name,,,ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область МестоположениеКомпании

Процедура ОтправитьМестоположениеКомпании(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ШиротаМеста_latitude = ВКМ_СлужебныеТелеграм.ПолучитьШиротуМестаКомпанииТелеграм();
	ДолготаМеста_longitude = ВКМ_СлужебныеТелеграм.ПолучитьДолготуМестаКомпанииТелеграм();
	НазваниеМеста_title = ВКМ_СлужебныеТелеграм.ПолучитьНазваниеКомпанииТелеграм();
	АдресМеста_address = ВКМ_СлужебныеТелеграм.ПолучитьАдресКомпанииТелеграм();
	
	ВКМ_МетодыТелеграм.ОтправитьМесторасположение_sendVenue(ИдентификаторЧата_chat_id,
		ШиротаМеста_latitude, ДолготаМеста_longitude, НазваниеМеста_title, АдресМеста_address,,,
		ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область РеквизитыКомпании

Процедура ОтправитьРеквизитыКомпании(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ИмяФайла = СтрШаблон("%1document.pdf", КаталогВременныхФайлов());
	МакетРеквизитыКомпании = ПолучитьОбщийМакет("ВКМ_РеквизитыКомпанииТелеграм");
	МакетРеквизитыКомпании.Записать(ИмяФайла);
	
	ДокументДляОтправки_document = Неопределено;
	ПодписьДокумента_caption = "%F0%9F%93%87 Реквизиты компании";
	
	ВКМ_МетодыТелеграм.ОтправитьДокумент_sendDocument(ИмяФайла, ИдентификаторЧата_chat_id, ДокументДляОтправки_document,
		ПодписьДокумента_caption,, ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область ОбразецЛистаУчета

Процедура ОтправитьОбразецЛистаУчета(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id)
	
	ИмяФайла = СтрШаблон("%1document.doc", КаталогВременныхФайлов());
	МакетОбразецЛистаУчета = ПолучитьОбщийМакет("ВКМ_ОбразецЛистаУчетаТелеграм");
	МакетОбразецЛистаУчета.Записать(ИмяФайла);
	
	ДокументДляОтправки_document = Неопределено;
	ПодписьДокумента_caption = "%F0%9F%93%8B Образец листа учёта рабочего времени";
	
	ВКМ_МетодыТелеграм.ОтправитьДокумент_sendDocument(ИмяФайла, ИдентификаторЧата_chat_id, ДокументДляОтправки_document,
		ПодписьДокумента_caption,, ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#Область СообщениеОбщегоОтвета

Процедура ОтправитьСообщениеОбщегоОтвета(ИдентификаторЧата_chat_id, ИдентификаторОтветаНаСообщение_reply_to_message_id, ТекстСообщенияОтправителя)
	
	ТекстПриветствие = "<b>%F0%9F%92%A1 Я чат бот! Приветствую вас!</b>";
	ТекстЭхоБот = СтрШаблон("<i>Вы написали: %1</i>", ТекстСообщенияОтправителя);
	ТекстПредупреждение = "<b>Данный чат бот не предназначен для общения!</b>";
	ТекстКомандаСтарт = "<b>Получить инструкцию можно по команде /start</b>";
	
	ТекстСообщения_text = СтрШаблон("%1%2%3%4%5%6%7",ТекстПриветствие, Символы.ПС, ТекстЭхоБот,
	Символы.ПС, ТекстПредупреждение, Символы.ПС, ТекстКомандаСтарт);
	
	ЯзыкРазметкиТекста_parse_mode = "HTML";
	
	ВКМ_МетодыТелеграм.ОтправитьСообщение_sendMessage(ИдентификаторЧата_chat_id, ТекстСообщения_text,
		ЯзыкРазметкиТекста_parse_mode,,, ИдентификаторОтветаНаСообщение_reply_to_message_id);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

