
#Область ПрограммныйИнтерфейс

// Доработать форму справочника договоры контрагентов.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ДоработатьФормуСправочникаДоговорыКонтрагентов(Форма) Экспорт
//ВКМ_Захарова: Добавить группу, элементы, реквизит на Форму Справочника "Договоры контрагентов" и Установить действия
	
	ЭлементыФормы = Форма.Элементы;
	
	ИмяГруппы = "ГруппаАбоненскоеОбслуживание";
	ГруппаФормы = ЭлементыФормы.Найти(ИмяГруппы);
	
	Если ГруппаФормы = Неопределено Тогда
		
		ДобавитьГруппуАбоненскоеОбслуживание(Форма);
		
		ДобавитьРеквизитФормыПериодДействияДоговора(Форма);
		
		ИмяЭлемента_ПериодДействияДоговора = "ПериодДействияДоговора";
		ПутьКДанным_ПериодДействияДоговора = "ВКМ_ПериодДействияДоговора";
		Элемент_ПериодДействияДоговора = ДобавитьНовыйЭлемент(ЭлементыФормы, ИмяЭлемента_ПериодДействияДоговора,
			ПутьКДанным_ПериодДействияДоговора);
		Элемент_ПериодДействияДоговора.УстановитьДействие("ПриИзменении", "Подключаемый_ВКМ_ПериодДействияДоговораПриИзменении");
		
		ИмяЭлемента_СуммаАбонентскойПлаты = "СуммаАбонентскойПлаты";
		ПутьКДанным_СуммаАбонентскойПлаты = "Объект.ВКМ_СуммаАбонентскойПлаты";
		ДобавитьНовыйЭлемент(ЭлементыФормы, ИмяЭлемента_СуммаАбонентскойПлаты, ПутьКДанным_СуммаАбонентскойПлаты);
		
		ИмяЭлемента_СтоимостьЧасаРаботы = "СтоимостьЧасаРаботы";
		ПутьКДанным_СтоимостьЧасаРаботы = "Объект.ВКМ_СтоимостьЧасаРаботы";
		ДобавитьНовыйЭлемент(ЭлементыФормы, ИмяЭлемента_СтоимостьЧасаРаботы, ПутьКДанным_СтоимостьЧасаРаботы);
		
	КонецЕсли;
	
//ВКМ_Захарова
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьГруппуАбоненскоеОбслуживание(Форма)
//ВКМ_Захарова: Добавить на ФормуЭлементаСправочника "ДоговорыКонтрагентов" НовуюГруппу
	
	ГруппаАбоненскоеОбслуживание =Форма.Элементы.Добавить("ГруппаАбоненскоеОбслуживание",Тип("ГруппаФормы"),Форма);
	ГруппаАбоненскоеОбслуживание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаАбоненскоеОбслуживание.Отображение = ОтображениеОбычнойГруппы.СлабоеВыделение;
	ГруппаАбоненскоеОбслуживание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаАбоненскоеОбслуживание.ОтображатьЗаголовок = Ложь;
	ГруппаАбоненскоеОбслуживание.ЦветФона = WebЦвета.Лазурный;
	
//ВКМ_Захарова
КонецПроцедуры

Процедура ДобавитьРеквизитФормыПериодДействияДоговора(Форма)
//ВКМ_Захарова: Добавить на ФормуЭлементаСправочника "ДоговорыКонтрагентов" НовыйРеквизит
	
	ДобавляемыеРеквизиты = Новый Массив;
	ИмяРеквизита = "ВКМ_ПериодДействияДоговора";
	ТипРеквизита = Новый ОписаниеТипов("СтандартныйПериод");
	ЗаголовокРеквизита = "Период действия договора";
	РеквизитПериодДействияДоговора = Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита,, ЗаголовокРеквизита, Истина);
	ДобавляемыеРеквизиты.Добавить(РеквизитПериодДействияДоговора);
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
//ВКМ_Захарова
КонецПроцедуры

Функция ДобавитьНовыйЭлемент(ЭлементыФормы, ИмяЭлемента, ПутьКДанным)
//ВКМ_Захарова: Добавить на ФормуЭлементаСправочника "ДоговорыКонтрагентов" НовыйЭлемент
	
	НовыйЭлемент = ЭлементыФормы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), ЭлементыФормы.ГруппаАбоненскоеОбслуживание);
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода; 
	НовыйЭлемент.ПутьКДанным = ПутьКДанным;
	
	Возврат НовыйЭлемент;
	
//ВКМ_Захарова
КонецФункции

#КонецОбласти

