
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СформироватьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДиаграммаПоПериодамОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаПоКонтрагентамОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьНаСервере()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	СхемаКомпоновки = ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	ДанныеРасшифровкиКомпоновкиДанных = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СхемаКомпоновки,
		Отчет.КомпоновщикНастроек.ПолучитьНастройки(), ДанныеРасшифровкиКомпоновкиДанных);
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных,, ДанныеРасшифровкиКомпоновкиДанных, Истина);
	ПроцессорВыводаРезультата = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	
	РезультатОтчета = Новый ТабличныйДокумент;
	
	ПроцессорВыводаРезультата.УстановитьДокумент(РезультатОтчета);
	ПроцессорВыводаРезультата.Вывести(ПроцессорКомпоновкиДанных);
	
	ЗаполнитьДиаграммы(РезультатОтчета, ДанныеРасшифровкиКомпоновкиДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммы(РезультатОтчета, ДанныеРасшифровкиКомпоновкиДанных)
	
	ДиаграммаПоПериодам = РезультатОтчета.Рисунки[0].Объект;
	
	Если ДанныеРасшифровкиПоПериодам = "" Тогда
		ДанныеРасшифровкиПоПериодам = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновкиДанных, УникальныйИдентификатор);
	Иначе
		ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновкиДанных, ДанныеРасшифровкиПоПериодам);
	КонецЕсли;
	
	
	ДиаграммаПоКонтрагентам = РезультатОтчета.Рисунки[1].Объект;
	
	Если ДанныеРасшифровкиПоКонтрагентам = "" Тогда
		ДанныеРасшифровкиПоКонтрагентам = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновкиДанных, УникальныйИдентификатор);
	Иначе
		ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновкиДанных, ДанныеРасшифровкиПоКонтрагентам);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти