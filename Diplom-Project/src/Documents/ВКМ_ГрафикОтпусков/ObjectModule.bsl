
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьПериодыТаблицыОтпускаСотрудников(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения_ОтпускаСотрудников();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПериодыТаблицыОтпускаСотрудников(Отказ)
	
	ВыбранныйГод = Год(Год);
	
	Для Каждого Строка Из ОтпускаСотрудников Цикл
		
		Если ЗначениеЗаполнено(Строка.ДатаНачала) Или ЗначениеЗаполнено(Строка.ДатаОкончания) Тогда
			
			Если Год(Строка.ДатаНачала) <> ВыбранныйГод Тогда
				ГодВТаблице = Формат(Строка.ДатаНачала, "ДФ=гггг");
				ГодДокумента = Формат(Год, "ДФ=гггг");
				ТекстСообщения = СтрШаблон("%1 год не соответствует году документа (%2)", ГодВТаблице, ГодДокумента);
				ПолеСообщения = СтрШаблон("ОтпускаСотрудников[%1].ДатаНачала", Строка.НомерСтроки - 1);;
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ПолеСообщения);
				Отказ = Истина;
			КонецЕсли;
			
			Если Год(Строка.ДатаОкончания) <> ВыбранныйГод Тогда
				ГодВТаблице = Формат(Строка.ДатаОкончания, "ДФ=гггг");
				ГодДокумента = Формат(Год, "ДФ=гггг");
				ТекстСообщения = СтрШаблон("%1 год не соответствует году документа (%2)", ГодВТаблице, ГодДокумента);
				ПолеСообщения = СтрШаблон("ОтпускаСотрудников[%1].ДатаОкончания", Строка.НомерСтроки - 1);;
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, ПолеСообщения);
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвижения_ОтпускаСотрудников()
	
	Движения.ВКМ_ОтпускаСотрудников.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала КАК Период,
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник КАК Сотрудник,
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.КоличествоДней КАК КоличествоДнейПлан
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
	|ГДЕ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.КоличествоДней";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		//Регистр накопления "ВКМ_ОтпускаСотрудников"
		Движение = Движения.ВКМ_ОтпускаСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
