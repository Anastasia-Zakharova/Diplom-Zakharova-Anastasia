
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Заполняем Цвет в реквизит формы "ЦветДокументаКалендарь" для Планировщика Обработка "Календарь специалиста"
	СохраняемЦветДокументаДляПланировщика(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Отказ от записи документа с незаполненными обязательными реквизитами
	ОтказОтЗаписиНезаполненногоДокумента(Отказ);
	
	// Заполняем Цвет в реквизит объект "ЦветДокументаКалендарь" для Планировщика Обработка "Календарь специалиста"
	ФормируемЦветДокументаДляПланировщика(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Оповещение для Планировщика Обработка "Календарь специалиста"
	Оповестить("ИзменениеЗаявки_ОбслуживаниеКлиента");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеПроблемыНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	//Сделать реквизит ОписаниеПроблемы многострочным и компактным
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования,
		ЭтотОбъект, "Объект.ОписаниеПроблемы", "Описание проблемы");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ПланировщикКалендарь

&НаСервере
Процедура СохраняемЦветДокументаДляПланировщика(ТекущийОбъект)
	
	ЦветДокументаКалендарь = ТекущийОбъект.ЦветДокументаКалендарь.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ФормируемЦветДокументаДляПланировщика(ТекущийОбъект)
	
	ТекущийОбъект.ЦветДокументаКалендарь = Новый ХранилищеЗначения(ЦветДокументаКалендарь);
	
КонецПроцедуры

#КонецОбласти

#Область ОтказОтЗаписиНезаполненногоДокумента

&НаСервере
Процедура ОтказОтЗаписиНезаполненногоДокумента(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Клиент) Тогда
		ОбщегоНазначения.СообщитьПользователю("Заполните клиета в документе",,"Объект.Клиент");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Договор) Тогда
		ОбщегоНазначения.СообщитьПользователю("Заполните договор в документе",,"Объект.Договор");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Специалист) Тогда
		ОбщегоНазначения.СообщитьПользователю("Заполните специалиста в документе",, "Объект.Специалист");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаПроведенияРабот) Тогда
		ОбщегоНазначения.СообщитьПользователю("Заполните дату проведения работ в документе",, "Объект.ДатаПроведенияРабот");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
