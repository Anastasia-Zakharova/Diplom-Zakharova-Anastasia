
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПроверитьЗаполениеТаблицыВыплаты(Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДвижения_ВзаиморасчетыССотрудниками();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Замечание №3. Перемещение алгоритма заполнения в модуль объекта
Процедура ЗаполнитьДокументВыплатаЗарплаты() Экспорт
	
	Выплаты.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник КАК Сотрудник,
		|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&ДатаОстатков, ) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки";
	
	ГраницаВремени = Новый Граница(КонецМесяца(Дата), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("ДатаОстатков", ГраницаВремени);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		ДатаДокумента = Формат(КонецМесяца(Дата), "ДЛФ=DD");
		ТекстСообщения = СтрШаблон("На конец месяца даты документа (%1) нет данных о выплатах сотрудникам", ДатаДокумента);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Выплаты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполениеТаблицыВыплаты(Отказ)
	
	Если Выплаты.Количество() = 0 Тогда
		ТекстСообщения = "Заполните документ ""Выплата зарплаты""";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект.Выплаты");
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьДвижения_ВзаиморасчетыССотрудниками()
	
	//Регистр накопления "Взаиморасчеты с сотрудниками"
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_ВыплатаЗарплатыВыплаты.Сумма) КАК Сумма
		|ИЗ
		|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
		|ГДЕ
		|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
